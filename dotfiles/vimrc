
set nocompatible

" Packages {{{
call plug#begin('~/.vim/plugged')
" Syntax {{{

" syntax checking
Plug 'scrooloose/syntastic'
let g:syntastic_python_checkers = ['flake8']
" }}}

" Colorscheme {{{
Plug 'morhetz/gruvbox'
" }}}

" UI {{{
Plug 'scrooloose/nerdtree'
Plug 'bling/vim-airline'
Plug 'ctrlpvim/ctrlp.vim'
Plug 'junegunn/goyo.vim'
" }}}
" Utility {{{
Plug 'danro/rename.vim'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-dispatch'
" }}}
" Edit {{{
Plug 'tpope/vim-surround'
Plug 'Raimondi/delimitMate'
" }}}
" Git {{{
Plug 'tpope/vim-fugitive'
" Git extension
Plug 'gregsexton/gitv'
" show diff on the left
Plug 'airblade/vim-gitgutter'
" }}}
" Snippet {{{

" snippet engine.
Plug 'SirVer/ultisnips'
" actual snippets
Plug 'honza/vim-snippets'

" }}}
" Python {{{
Plug 'nvie/vim-flake8'
Plug 'ivanov/vim-ipython'
Plug 'hynek/vim-python-pep8-indent'
Plug 'davidhalter/jedi-vim'
" }}}
" Rust {{{
Plug 'rust-lang/rust.vim'

" }}}
" HTML {{{
Plug 'wavded/vim-stylus'

" }}}
" CoffeeScript {{{
Plug 'kchmck/vim-coffee-script'
" }}}
" Markdown {{{
Plug 'tpope/vim-markdown'
" }}}
call plug#end()
" }}}

" General {{{

let mapleader = " "
let maplocalleader = "\\"

" enable 256 colors
set t_Co=256

filetype plugin indent on
syntax on

if has("gui_running")
    set guifont=Bitstream\ Vera\ Sans\ Mono\ 11
endif

set background=dark
colorscheme gruvbox

" Set 5 lines to the cursor - when moving vertically
set scrolloff=5

" This orders Vim to open the buffer.
set switchbuf=useopen

" }}}

" Settings {{{
set autoread
set backspace=indent,start
set binary
set cinoptions=:0,(s,u0,U1,g0,t0
set completeopt=menuone,preview
set encoding=utf-8
set hidden
set history=1000
set incsearch
set laststatus=2
set list

" Don't redraw while executing macros
set nolazyredraw

" Disable the gui toolbar
set guioptions-=T
" Disable the gui menu bar
set guioptions-=m

set listchars=tab:▸\ ,extends:❯,precedes:❮
set showbreak=↪
set breakindent

set notimeout
set ttimeout
set ttimeoutlen=10
set updatetime=250

" backups {{{
set undodir=~/.vim/tmp/undo//     " undo files
set undofile
set undolevels=3000
set undoreload=10000
set backupdir=~/.vim/tmp/backup// " backups
set directory=~/.vim/tmp/swap//   " swap files
set backup
set noswapfile
" _ }}}

set modelines=0
set noeol
set ruler
if executable('/bin/zsh')
  set shell=/bin/zsh
endif
set showcmd

set exrc
set secure

set matchtime=2

set completeopt=longest,menuone,preview

" White characters {{{
set autoindent
set tabstop=4
set softtabstop=4
set textwidth=80
set shiftwidth=4
set expandtab
set wrap
set formatoptions=qrn1
set linespace=2
set colorcolumn=80
" }}}

set wildignore=.svn,CVS,.git,.hg,*.o,*.a,*.class,*.mo,*.la,*.so,*.obj,*.swp,*.jpg,*.png,*.xpm,*.gif,.DS_Store,*.aux,*.out,*.toc,tmp,*.scssc
set wildmenu

set dictionary=/usr/share/dict/words
" }}}

" Mappings {{{

" disable arrows
noremap <left> <nop>
noremap <up> <nop>
noremap <down> <nop>
noremap <right> <nop>

inoremap <left> <nop>
inoremap <up> <nop>
inoremap <down> <nop>
inoremap <right> <nop>

" Movement in insert mode

inoremap <C-h> <left>
inoremap <C-l> <right>
inoremap <C-j> <down>
inoremap <C-k> <up>

" Buffers
nnoremap ]b :bnext<cr>
nnoremap [b :bprev<cr>


" Yank from current cursor position to end of line
map Y y$

" clear highlight after search
noremap <silent><Leader>/ :nohls<CR>

nmap <silent> <leader>ih :set invhlsearch<CR>
nmap <silent> <leader>il :set invlist<CR>
nmap <silent> <leader>in :set invnumber<CR>
nmap <silent> <leader>ip :set invpaste<CR>
nmap <silent> <leader>ii :set invrelativenumber<CR>

command! W :w

" fast save
nnoremap <Leader>w :w<CR>

" Source current line
vnoremap <leader>L y:execute @@<cr>
" Source visual selection
nnoremap <leader>L ^vg_y:execute @@<cr>

" Fast saving and closing current buffer without closing windows displaying the
" buffer
nmap <leader>wq :w!<cr>:Bclose<cr>

" newline in normal mode
noremap <S-Enter> m`O<Esc>j``
noremap <CR> m`o<Esc>k``

" }}}

" NerdTree {{{

nmap <C-n> :NERDTreeToggle<CR>
" Disable the scrollbars (NERDTree)
set guioptions-=r
set guioptions-=L

" }}}

" Airline {{{

let g:airline#extensions#tabline#enabled = 1
" }}}

" Git {{{

nnoremap <leader>gb :Gblame<cr>
nnoremap <leader>gd :Gdiff<cr>
nnoremap <leader>gs :Gstatus<cr>
nnoremap <leader>gx :Gremove<cr>
nnoremap <leader>gm :Gmove<space>
nnoremap <leader>gr :Gread<cr>
nnoremap <leader>ga :Gwrite<cr>
nnoremap <leader>gc :Gcommit<cr>
nnoremap <leader>gp !git push<cr>
nnoremap <leader>gl !git pull<cr>

nmap <leader>g :Ggrep
" ,f for global git serach for word under the cursor (with highlight)
nmap <leader>f :let @/="\\<<C-R><C-W>\\>"<CR>:set hls<CR>:silent Ggrep -w "<C-R><C-W>"<CR>:ccl<CR>:cw<CR><CR>
" same in visual mode
:vmap <leader>f y:let @/=escape(@", '\\[]$^*.')<CR>:set hls<CR>:silent Ggrep -F "<C-R>=escape(@", '\\"#')<CR>"<CR>:ccl<CR>:cw<CR><CR>

autocmd FileType gitcommit set tw=68 spell
autocmd FileType gitcommit setlocal foldmethod=manual

" }}}

" UtilSnips {{{
" Trigger configuration. Do not use <tab> if you use https://github.com/Valloric/YouCompleteMe.
let g:UltiSnipsExpandTrigger="<tab>"
let g:UltiSnipsJumpForwardTrigger="<c-b>"
let g:UltiSnipsJumpBackwardTrigger="<c-z>"

" If you want :UltiSnipsEdit to split your window.
let g:UltiSnipsEditSplit="vertical"

" }}}

" Jedi {{{
let g:jedi#goto_command = "<leader>d"
let g:jedi#goto_assignments_command = "<leader>a"
let g:jedi#goto_definitions_command = ""
let g:jedi#documentation_command = "K"
let g:jedi#usages_command = "<leader>n"
let g:jedi#completions_command = "<C-Space>"
let g:jedi#rename_command = "<leader>r"
let g:jedi#force_py_version = 3
" }}}

" Goyo {{{
nnoremap <leader>z :Goyo<CR>
" }}}

" Cursorline {{{
" Only show cursorline in the current window and in normal mode.
augroup cline
    au!
    au WinLeave * set nocursorline
    au WinEnter * set cursorline
    au InsertEnter * set nocursorline
    au InsertLeave * set cursorline
augroup END
" }}}

" Trailing whitespace {{{
" Only shown when not in insert mode so I don't go insane.
augroup trailing
    au!
    au InsertEnter * :set listchars-=trail:␣
    au InsertLeave * :set listchars+=trail:␣
augroup END

" Remove trailing whitespaces when saving
" Wanna know more? http://vim.wikia.com/wiki/Remove_unwanted_spaces
" If you want to remove trailing spaces when you want, so not automatically,
" see
" http://vim.wikia.com/wiki/Remove_unwanted_spaces#Display_or_remove_unwanted_whitespace_with_a_script.
autocmd BufWritePre * :%s/\s\+$//e

" }}}

" Searching {{{

" sane regexes
nnoremap / /\v
vnoremap / /\v

set ignorecase
set smartcase
set showmatch
set gdefault
set hlsearch

" clear search matching
noremap <leader>c :noh<cr>:call clearmatches()<cr>

" Don't jump when using * for search
nnoremap * *<c-o>

" Keep search matches in the middle of the window.
nnoremap n nzzzv
nnoremap N Nzzzv

" Same when jumping around
nnoremap g; g;zz
nnoremap g, g,zz

" Open a Quickfix window for the last search.
nnoremap <silent> <leader>? :execute 'vimgrep /'.@/.'/g %'<CR>:copen<CR>

" Highlight word {{{

nnoremap <silent> <leader>hh :execute 'match InterestingWord1 /\<<c-r><c-w>\>/'<cr>
nnoremap <silent> <leader>h1 :execute 'match InterestingWord1 /\<<c-r><c-w>\>/'<cr>
nnoremap <silent> <leader>h2 :execute '2match InterestingWord2 /\<<c-r><c-w>\>/'<cr>
nnoremap <silent> <leader>h3 :execute '3match InterestingWord3 /\<<c-r><c-w>\>/'<cr>

" }}}

" }}}

" Past & Copy {{{

" easy paste
nnoremap <leader>p "+gp
nnoremap <leader>P "+gP

" easy copy
nnoremap <leader>y "+y
nnoremap <leader>Y "+Y

" }}}

" Navigation & UI {{{

" Begining & End of line in Normal mode
noremap H ^
noremap L g_

" more natural movement with wrap on
nnoremap j gj
nnoremap k gk
vnoremap j gj
vnoremap k gk

" move half page
" nnoremap js <C-d>
" nnoremap ks <C-u>

" Easy buffer navigation
noremap <leader>bp :bprevious<cr>
noremap <leader>bn :bnext<cr>

" Move in autocomplete dropdown menu
inoremap <expr> j ((pumvisible())?("\<C-n>"):("j"))
inoremap <expr> k ((pumvisible())?("\<C-p>"):("k"))


" <tab> / <s-tab> | Circular windows navigation
nnoremap <tab>   <c-w>w
nnoremap <S-tab> <c-w>W

" Moving lines
nnoremap <silent> <C-k> :move-2<cr>
nnoremap <silent> <C-j> :move+<cr>
nnoremap <silent> <C-h> <<
nnoremap <silent> <C-l> >>
xnoremap <silent> <C-k> :move-2<cr>gv
xnoremap <silent> <C-j> :move'>+<cr>gv
xnoremap <silent> <C-h> <gv
xnoremap <silent> <C-l> >gv

" Reselect visual block after indent/outdent
xnoremap < <gv
xnoremap > >gv


" }}}

" Folding {{{

set foldlevelstart=0
set foldmethod=syntax

"  to toggle folds.
nnoremap <space>t za
vnoremap <space>t za

" Make zO recursively open whatever top level fold we're in, no matter where the
" cursor happens to be.
nnoremap zO zCzO

" }}}

" Quick editing {{{

nnoremap <leader>ev <C-w>s<C-w>j:e $MYVIMRC<cr>
nnoremap <leader>es <C-w>s<C-w>j:e ~/.vim/snippets/<cr>
nnoremap <leader>eg <C-w>s<C-w>j:e ~/.gitconfig<cr>
nnoremap <leader>ez <C-w>s<C-w>j:e ~/.zshrc<cr>
nnoremap <leader>et <C-w>s<C-w>j:e ~/.tmux.conf<cr>


" showmarks
let g:showmarks_enable = 1
hi! link ShowMarksHLl LineNr
hi! link ShowMarksHLu LineNr
hi! link ShowMarksHLo LineNr
hi! link ShowMarksHLm LineNr

" }}}

" AUTOCMD {{{

augroup vimrc
    autocmd!
    au FileType vim setlocal foldmethod=marker
    au BufWritePost vimrc,.vimrc nested if expand('%') !~ 'fugitive' | source % | endif

    " Save when losing focus
    au FocusLost    * :silent! wall
    au BufNewFile,BufReadPost *.html setl shiftwidth=2 tabstop=2 softtabstop=2 expandtab
augroup END

" }}}

" Extensions {{{

" _. Scratch {{{
source ~/.vim/functions/scratch_toggle.vim
" }}}

" _. Buffer Handling {{{
source ~/.vim/functions/buffer_handling.vim
" }}}

" _. Tab {{{
source ~/.vim/functions/insert_tab_wrapper.vim
" }}}

" _. Text Folding {{{
source ~/.vim/functions/my_fold_text.vim
" }}}

" _. Gist {{{
" Send visual selection to gist.github.com as a private, filetyped Gist
" Requires the gist command line too
vnoremap <leader>G :w !gist -p -t %:e \| xclip -i -selection clipboard <cr>
" }}}

" Show syntax highlighting groups for word under cursor {{{
nmap <F2> :call <SID>SynStack()<CR>
function! <SID>SynStack()
  if !exists("*synstack")
    return
  endif
  echo map(synstack(line('.'), col('.')), 'synIDattr(v:val, "name")')
endfunc
" }}}

" }}}